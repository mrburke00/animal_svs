'''
Snakefile 

Authors:
Layer Lab

Adapted from supplementary materials of:

Bertolotti, Alicia C., et al. 'The structural variation landscape
in 492 Atlantic salmon genomes.' bioRxiv (2020).
'''
import os
from snakemake.remote.GS import RemoteProvider as GSRemoteProvider

# setup the connection to google cloud storage
# NOTE: we are assuming all files are then in the same bucket
GS = GSRemoteProvider()

# helper function to make all files GS files
to_gs = lambda f: GS.remote(config['run']['deployment']['bucket_name'] + '/' + f)

# tell snakemake that the config file exits
configfile: 'config.yaml'

# extract the sample name. 
sample_name = os.path.basename(config['input']['forward']) \
if config['input']['sample_name'] == '' else config['input']['sample_name']

rule all:
    input:
        to_gs(os.path.join(config['run']['output_dir'], 'localTest-indexcov', 'index.html')),
        to_gs(os.path.join(config['run']['output_dir'], 'smoove', 'results', 'genotyped', 'localTest-smoove.genotyped.vcf.gz'))
        

# indexing the reference must be done at all time
rule bwa_index_reference: 
    input:
        to_gs(config['input']['reference'])
    output: 
        os.path.join(config['run']['temp_dir'], os.path.basename(config['input']['reference']))
    params:
        prefix = os.path.join(config['run']['temp_dir'], os.path.basename(config['input']['reference'])),
        cp_path = os.path.join(config['run']['temp_dir'], config['input']['reference'])
    log:
        os.path.join(config['run']['logs_dir'], 'bwa_index_reference.log')
    conda:
        'envs/bwa_idx_ref.yaml'
    shell:
        # copy fa from main to temp for later
        'gsutil cp gs://{input} {params.cp_path} && '
        'bwa index -p {params.prefix} {params.cp_path} &> {log}' 

rule samtools_faidx:
    input:
        to_gs(config['input']['reference'])
    output: 
        os.path.join(config['run']['temp_dir'], os.path.basename(config['input']['reference']) + '.fai')
    log: 
        os.path.join(config['run']['logs_dir'], 'samtools_faidx.log')
    conda: 
        'envs/samtools.yaml'
    shell:
        'samtools faidx {input} -o {output}'

rule bwa_map:
    input:
        # "reverse" is a keyword internally to snakemake, so we have to shorten it to rev
        reference = rules.bwa_index_reference.output,
        forward = to_gs(config['input']['samples']['forward']),
        rev = to_gs(config['input']['samples']['reverse'])
    output:
        os.path.join(config['run']['temp_dir'], 'mapped_reads', '{sample_name}.bam')
    params:
        rg = '@RG\\tID:{sample_name}\\tSM:{sample_name}\\tLB:lib1' 
    log:
        os.path.join(config['run']['logs_dir'], 'bwa_map_{sample_name}.log')
    conda:
        'envs/map.yaml'
    threads:
        # its ok if this is more threads than are available to us. Snakemake
        # does the operation: threads = min(threads, cores) so we can't go over
        threads = workflow.cores
    shell:
        '(bwa mem -R "{params.rg}" -t {threads} {input.reference}'
        ' {input.forward} {input.rev} | samtools view -Sb > {output})'
        ' 2> {log}'

rule samtools_sort:
    input:
        rules.bwa_map.output
    output:
        os.path.join(config['run']['temp_dir'], 'sorted_reads', '{sample_name}.bam')
    params:
        samtools_temp = os.path.join(config['run']['temp_dir'], 'sorted_reads', sample_name)
    log:
        os.path.join(config['run']['logs_dir'], 'samtools_sort_{sample_name}.log')
    conda:
        'envs/samtools.yaml'
    shell:
        'samtools sort -T {params.samtools_temp} -O bam {input} > {output} 2> {log}'

rule samtools_index:
    input:
        rules.samtools_sort.output
    output:
        os.path.join(config['run']['temp_dir'], 'sorted_reads', '{sample_name}.bam.bai')
    log:
        os.path.join(config['run']['logs_dir'], 'samtools_index_{sample_name}.log')
    conda:
        'envs/samtools.yaml'
    shell:
        'echo {input} && samtools index {input}'

rule goleft_indexcov:
    input:
        bam = rules.samtools_sort.output,
        # NOTE: keep the bai here. Although we don't use it, its necessary to force 
        # snakemake to run samtools_index to get the bai file before we can run goleft
        bai = rules.samtools_index.output
    output:
        to_gs(os.path.join(config['run']['output_dir'], '{sample_name}-indexcov', 'index.html'))
    params:
        output_dir = to_gs(os.path.join(config['run']['output_dir'], '{sample_name}-indexcov'))
    log:
        os.path.join(config['run']['logs_dir'], 'goleft_indexcov_{sample_name}.log')
    conda:
        'envs/goleft.yaml'
    shell:
        'goleft indexcov -d {params.output_dir} {input.bam} 2> {log}'

#----------              NOTE                    ----------#
'''
In the paper, they separate the two snakefiles and call a python script in 
the middle the extract gap regions in the genome and convert to a BED file. 
That script will be integrated here. This note is so that I don't forget
'''

rule extract_gap_regions:
    input:
        to_gs(config['input']['reference'])
    output:
        os.path.join(config['run']['temp_dir'], 'output_ranges.bed')
    log:
        os.path.join(config['run']['logs_dir'], 'extract_gap_regions.log')
    script:
        './scripts/extractGapRegions.py'

rule call:
    input:
        bam = rules.samtools_sort.output,
        bai = rules.samtools_index.output,
        exclude = rules.extract_gap_regions.output, 
        ref = rules.bwa_index_reference.output, 
        ref_idxed = rules.samtools_faidx.output	
    output:
        os.path.join(config['run']['temp_dir'], 'smoove', 'results', 'called', '{sample_name}-smoove.genotyped.vcf.gz')
    params:
        output_dir = os.path.join(config['run']['temp_dir'], 'smoove', 'results', 'called')
    log:
         os.path.join(config['run']['logs_dir'], 'smoove_call_{sample_name}.log')
    conda:
        'envs/smoove.yaml'
    shell:
        'smoove call --outdir {params.output_dir} --exclude {input.exclude}'
        ' -name {sample_name} --fasta {input.ref} -p 1 --genotype {input.bam}'

rule genotype:
    input: 
        vcf = rules.call.output,
        bam = rules.samtools_sort.output, 
        bai = rules.samtools_index.output, 
        ref = rules.bwa_index_reference.output, 
        ref_idxed = rules.samtools_faidx.output
    output:
        to_gs(os.path.join(config['run']['output_dir'], 'smoove', 'results', 'genotyped', '{sample_name}-smoove.genotyped.vcf.gz'))
    log:
        os.path.join(config['run']['logs_dir'], 'smoove_genotype_{sample_name}.log')
    params:
        output_dir = to_gs(os.path.join(config['run']['output_dir'], 'smoove', 'results', 'genotyped'))
    conda:
        'envs/smoove.yaml'
    shell: 
        'smoove genotype -d -x -p 1 --name {sample_name} -outdir {params.output_dir}'
        ' --fasta {input.ref} --vcf {input.vcf} {input.bam}'
